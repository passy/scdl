version "2.0"

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.1-SNAPSHOT'
    }
}

apply plugin: 'android'
apply plugin: 'android-test'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url 'https://github.com/dahlgren/vpi-aar/raw/master'
    }

    maven {
        url 'https://raw.github.com/ark/ark/master/releases'
    }

    maven {
        url 'https://oss.sonatype.org/content/groups/staging/'
    }

    maven {
        url 'http://download.crashlytics.com/maven'
    }
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.1"

    defaultConfig {
        versionCode 1051
        versionName "1.7.6"
        minSdkVersion 14
        targetSdkVersion 18
    }

    signingConfigs {
        release {
            if ('assembleRelease' in gradle.startParameter.taskNames) {
                storeFile file(System.getenv('ANDROID_KEYSTORE'))
                storePassword System.console().readLine("\n\$ Enter store password: ")
                keyAlias System.getenv('ANDROID_KEY_ALIAS')
                keyPassword System.console().readLine("\n\$ Enter key password: ")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        play {
            buildConfig "public static final net.rdrei.android.scdl2.Config.MARKETPLACE_TYPE STORE = net.rdrei.android.scdl2.Config.MARKETPLACE_TYPE.GOOGLE_PLAY;", \
                "public static final boolean PAID_BUILD = false;"
        }

        amazon {
            buildConfig "public static final net.rdrei.android.scdl2.Config.MARKETPLACE_TYPE STORE = net.rdrei.android.scdl2.Config.MARKETPLACE_TYPE.AMAZON;", \
                "public static final boolean PAID_BUILD = true;"
        }
    }
}

dependencies {
    // Prod dependencies
    compile 'com.android.support:support-v13:+'
    compile 'com.android.support:support-v4:+'
    compile 'com.viewpagerindicator:library:2.4.2-SNAPSHOT'
    compile 'com.google.analytics:tracking:2.0beta5'
    compile 'com.google.android.admob:admob:6.4.1'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'net.rdrei.android.dirchooser:library:1.1-SNAPSHOT@aar'
    compile 'javax.inject:javax.inject:1'
    compile 'com.crashlytics.android:crashlytics:1.+'

    compile files('libs/guice-3.0-no_aop.jar')
    compile('org.roboguice:roboguice:2.0') {
        exclude group: 'com.google.inject'
    }
    compile('com.google.inject.extensions:guice-assistedinject:3.0') {
        exclude group: 'com.google.inject'
    }
    compile('de.keyboardsurfer.android.widget:crouton:1.8.1') {
        exclude group: 'com.google.android', module: 'support-v4'
    }

    // Test dependencies
    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
}
